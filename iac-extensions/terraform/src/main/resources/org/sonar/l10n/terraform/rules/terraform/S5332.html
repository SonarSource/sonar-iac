<p>Clear-text protocols such as <code>ftp</code>, <code>telnet</code> or non-secure <code>http</code> lack encryption of transported data, as well as
the capability to build an authenticated connection. It means that an attacker able to sniff traffic from the network can read, modify or corrupt the
transported content. These protocols are not secure as they expose applications to an extensive range of risks:</p>
<ul>
  <li> Sensitive data exposure </li>
  <li> Traffic redirected to a malicious endpoint </li>
  <li> Malware infected software update or installer </li>
  <li> Execution of client side code </li>
  <li> Corruption of critical information </li>
</ul>
<p>Even in the context of isolated networks like offline environments or segmented cloud environments, the insider threat exists. Thus, attacks
involving communications being sniffed or tampered with can still happen.</p>
<p>For example, attackers could successfully compromise prior security layers by:</p>
<ul>
  <li> Bypassing isolation mechanisms </li>
  <li> Compromising a component of the network </li>
  <li> Getting the credentials of an internal IAM account (either from a service account or an actual person) </li>
</ul>
<p>In such cases, encrypting communications would decrease the chances of attackers to successfully leak data or steal credentials from other network
components. By layering various security practices (segmentation and encryption, for example), the application will follow the
<em>defense-in-depth</em> principle.</p>
<p>Note that using the <code>http</code> protocol is being deprecated by <a
href="https://blog.mozilla.org/security/2015/04/30/deprecating-non-secure-http">major web browsers</a>.</p>
<p>In the past, it has led to the following vulnerabilities:</p>
<ul>
  <li> <a href="https://nvd.nist.gov/vuln/detail/CVE-2019-6169">CVE-2019-6169</a> </li>
  <li> <a href="https://nvd.nist.gov/vuln/detail/CVE-2019-12327">CVE-2019-12327</a> </li>
  <li> <a href="https://nvd.nist.gov/vuln/detail/CVE-2019-11065">CVE-2019-11065</a> </li>
</ul>
<h2>Ask Yourself Whether</h2>
<ul>
  <li> Application data needs to be protected against falsifications or leaks when transiting over the network. </li>
  <li> Application data transits over a network that is considered untrusted. </li>
  <li> Compliance rules require the service to encrypt data in transit. </li>
  <li> Your application renders web pages with a relaxed mixed content policy. </li>
  <li> OS level protections against clear-text traffic are deactivated. </li>
</ul>
<p>There is a risk if you answered yes to any of those questions.</p>
<h2>Recommended Secure Coding Practices</h2>
<ul>
  <li> Make application data transit over a secure, authenticated and encrypted protocol like TLS or SSH. Here are a few alternatives to the most
  common clear-text protocols:
    <ul>
      <li> Use<code>ssh</code> as an alternative to <code>telnet</code> </li>
      <li> Use <code>sftp</code>, <code>scp</code> or <code>ftps</code> instead of <code>ftp</code> </li>
      <li> Use <code>https</code> instead of <code>http</code> </li>
      <li> Use <code>SMTP</code> over <code>SSL/TLS</code> or <code>SMTP</code> with <code>STARTTLS</code> instead of clear-text SMTP </li>
    </ul>  </li>
  <li> Enable encryption of cloud components communications whenever itâ€™s possible. </li>
  <li> Configure your application to block mixed content when rendering web pages. </li>
  <li> If available, enforce OS level deativation of all clear-text traffic </li>
</ul>
<p>It is recommended to secure all transport channels (even local network) as it can take a single non secure connection to compromise an entire
application or system.</p>
<h2>Sensitive Code Example</h2>
<p>For <a href="https://aws.amazon.com/kinesis/">AWS Kinesis</a> Data Streams server-side encryption:</p>
<pre>
resource "aws_kinesis_stream" "sensitive_stream" {
    encryption_type = "NONE" # Sensitive
}
</pre>
<p>For <a href="https://aws.amazon.com/elasticache/">Amazon ElastiCache</a>:</p>
<pre>
resource "aws_elasticache_replication_group" "example" {
    replication_group_id = "example"
    replication_group_description = "example"
    transit_encryption_enabled = false  # Sensitive
}
</pre>
<p>For <a href="https://aws.amazon.com/ecs/">Amazon ECS</a>:</p>
<pre>
resource "aws_ecs_task_definition" "ecs_task" {
  family = "service"
  container_definitions = file("task-definition.json")

  volume {
    name = "storage"
    efs_volume_configuration {
      file_system_id = aws_efs_file_system.fs.id
      transit_encryption = "DISABLED"  # Sensitive
    }
  }
}
</pre>
<p>For <a href="https://docs.aws.amazon.com/opensearch-service/index.html">Amazon OpenSearch domains</a>:</p>
<pre>
resource "aws_elasticsearch_domain" "example" {
  domain_name = "example"
  domain_endpoint_options {
    enforce_https = false # Sensitive
  }
  node_to_node_encryption {
    enabled = false # Sensitive
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/msk/">Amazon MSK</a> communications between clients and brokers:</p>
<pre>
resource "aws_msk_cluster" "sensitive_data_cluster" {
    encryption_info {
        encryption_in_transit {
            client_broker = "TLS_PLAINTEXT" # Sensitive
            in_cluster = false # Sensitive
        }
    }
}
</pre>
<p>For <a href="https://docs.aws.amazon.com/elasticloadbalancing/latest/application/load-balancer-listeners.html">AWS Load Balancer Listeners</a>:</p>
<pre>
resource "aws_lb_listener" "front_load_balancer" {
  protocol = "HTTP" # Sensitive

  default_action {
    type = "redirect"

    redirect {
      protocol = "HTTP"
    }
  }
}
</pre>
<p>HTTP protocol is used for <a
href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/compute_region_backend_service">GCP Region Backend
Services</a>:</p>
<pre>
resource "google_compute_region_backend_service" "example" {
  name                            = "example-service"
  region                          = "us-central1"
  health_checks                   = [google_compute_region_health_check.region.id]
  connection_draining_timeout_sec = 10
  session_affinity                = "CLIENT_IP"
  load_balancing_scheme           = "EXTERNAL"
  protocol                        = "HTTP" # Sensitive
}
</pre>
<h2>Compliant Solution</h2>
<p>For <a href="https://aws.amazon.com/kinesis/">AWS Kinesis</a> Data Streams server-side encryption:</p>
<pre>
resource "aws_kinesis_stream" "compliant_stream" {
    encryption_type = "KMS"
}
</pre>
<p>For <a href="https://aws.amazon.com/elasticache/">Amazon ElastiCache</a>:</p>
<pre>
resource "aws_elasticache_replication_group" "example" {
    replication_group_id = "example"
    replication_group_description = "example"
    transit_encryption_enabled = true
}
</pre>
<p>For <a href="https://aws.amazon.com/ecs/">Amazon ECS</a>:</p>
<pre>
resource "aws_ecs_task_definition" "ecs_task" {
  family = "service"
  container_definitions = file("task-definition.json")

  volume {
    name = "storage"
    efs_volume_configuration {
      file_system_id = aws_efs_file_system.fs.id
      transit_encryption = "ENABLED"
    }
  }
}
</pre>
<p>For <a href="https://docs.aws.amazon.com/opensearch-service/index.html">Amazon OpenSearch domains</a>:</p>
<pre>
resource "aws_elasticsearch_domain" "example" {
  domain_name = "example"
  domain_endpoint_options {
    enforce_https = true
  }
  node_to_node_encryption {
    enabled = true
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/msk/">Amazon MSK</a> communications between clients and brokers, data in transit is encrypted by default,
allowing you to omit writing the <code>encryption_in_transit</code> configuration. However, if you need to configure it explicitly, this configuration
is compliant:</p>
<pre>
resource "aws_msk_cluster" "sensitive_data_cluster" {
    encryption_info {
        encryption_in_transit {
            client_broker = "TLS"
            in_cluster = true
        }
    }
}
</pre>
<p>For <a href="https://docs.aws.amazon.com/elasticloadbalancing/latest/application/load-balancer-listeners.html">AWS Load Balancer Listeners</a>:</p>
<pre>
resource "aws_lb_listener" "front_load_balancer" {
  protocol = "HTTP"

  default_action {
    type = "redirect"

    redirect {
      protocol = "HTTPS"
    }
  }
}
</pre>
<p>HTTPS protocol is used for <a
href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/compute_region_backend_service">GCP Region Backend
Services</a>:</p>
<pre>
resource "google_compute_region_backend_service" "example" {
  name                            = "example-service"
  region                          = "us-central1"
  health_checks                   = [google_compute_region_health_check.region.id]
  connection_draining_timeout_sec = 10
  session_affinity                = "CLIENT_IP"
  load_balancing_scheme           = "EXTERNAL"
  protocol                        = "HTTPS"
}
</pre>
<h2>Exceptions</h2>
<p>No issue is reported for the following cases because they are not considered sensitive:</p>
<ul>
  <li> Insecure protocol scheme followed by loopback addresses like 127.0.0.1 or <code>localhost</code> </li>
</ul>
<h2>See</h2>
<ul>
  <li> <a href="https://owasp.org/Top10/A02_2021-Cryptographic_Failures/">OWASP Top 10 2021 Category A2</a> - Cryptographic Failures </li>
  <li> <a href="https://www.owasp.org/index.php/Top_10-2017_A3-Sensitive_Data_Exposure">OWASP Top 10 2017 Category A3</a> - Sensitive Data Exposure
  </li>
  <li> <a href="https://mobile-security.gitbook.io/masvs/security-requirements/0x10-v5-network_communication_requirements">Mobile AppSec Verification
  Standard</a> - Network Communication Requirements </li>
  <li> <a href="https://owasp.org/www-project-mobile-top-10/2016-risks/m3-insecure-communication">OWASP Mobile Top 10 2016 Category M3</a> - Insecure
  Communication </li>
  <li> <a href="https://cwe.mitre.org/data/definitions/200">MITRE, CWE-200</a> - Exposure of Sensitive Information to an Unauthorized Actor </li>
  <li> <a href="https://cwe.mitre.org/data/definitions/319">MITRE, CWE-319</a> - Cleartext Transmission of Sensitive Information </li>
  <li> <a href="https://security.googleblog.com/2016/09/moving-towards-more-secure-web.html">Google, Moving towards more secure web</a> </li>
  <li> <a href="https://blog.mozilla.org/security/2015/04/30/deprecating-non-secure-http/">Mozilla, Deprecating non secure http</a> </li>
</ul>

