<p>Disabling logging of this component can lead to missing traceability in case of a security incident.</p>
<p>Logging allows operational and security teams to get detailed and real-time feedback on an information systemâ€™s events. The logging coverage
enables them to quickly react to events, ranging from the most benign bugs to the most impactful security incidents, such as intrusions.</p>
<p>Apart from security detection, logging capabilities also directly influence future digital forensic analyses. For example, detailed logging will
allow investigators to establish a timeline of the actions perpetrated by an attacker.</p>
<h2>Ask Yourself Whether</h2>
<ul>
  <li> This component is essential for the information system infrastructure. </li>
  <li> This component is essential for mission-critical functions. </li>
  <li> Compliance policies require this component to be monitored. </li>
</ul>
<p>There is a risk if you answered yes to any of those questions.</p>
<h2>Recommended Secure Coding Practices</h2>
<p>Enable the logging capabilities of this component.</p>
<h2>Sensitive Code Example</h2>
<p>For <a href="https://aws.amazon.com/s3/">Amazon S3 access requests</a>:</p>
<pre>
resource "aws_s3_bucket" "mynoncompliantbucket" { # Sensitive
  bucket = "mynoncompliantbucketname"
}
</pre>
<p>For <a href="https://aws.amazon.com/api-gateway/">Amazon API Gateway</a> stages:</p>
<pre>
resource "aws_api_gateway_stage" "api-v1" { # Sensitive
  deployment_id = aws_api_gateway_deployment.example.id
  rest_api_id   = aws_api_gateway_rest_api.example.id
  stage_name    = "v1-prod-api"
  xray_tracing_enabled = false # Sensitive
}
</pre>
<p>For <a href="https://aws.amazon.com/neptune/">Amazon Neptune</a> clusters:</p>
<pre>
resource "aws_neptune_cluster" "cluster" {
  enable_cloudwatch_logs_exports = []  # Sensitive
}
</pre>
<p>For <a href="https://aws.amazon.com/msk/">Amazon MSK</a> broker logs:</p>
<pre>
resource "aws_msk_cluster" "sensitive_msk" {
  cluster_name = "sensitive_msk"
  logging_info {
    broker_logs { # Sensitive
      firehose {
        enabled = false
      }
      s3 {
        enabled = false
      }
    }
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/amazon-mq/">Amazon MQ</a>:</p>
<pre>
resource "aws_mq_broker" "broker" {
  logs {  # Sensitive
    audit = false
    general = false
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/documentdb/">Amazon DocumentDB</a>:</p>
<pre>
resource "aws_docdb_cluster" "docdb_omitting_logs" { # Sensitive
  cluster_identifier = "DB Cluster Without Logs"
}
</pre>
<p>For <a href="https://aws.amazon.com/redshift/">Amazon Redshift</a>:</p>
<pre>
resource "aws_redshift_cluster" "cluster" {
  cluster_identifier = "redshift-cluster"

  logging {
    enable = false # Sensitive
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/global-accelerator/">Amazon Global Accelerator</a>:</p>
<pre>
resource "aws_globalaccelerator_accelerator" "accelerator" {
  attributes {
    flow_logs_enabled   = false  # Sensitive
    flow_logs_s3_bucket = "example-bucket"
    flow_logs_s3_prefix = "flow-logs/"
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/opensearch-service/">Amazon OpenSearch</a> service, or Amazon Elasticsearch service:</p>
<pre>
resource "aws_elasticsearch_domain" "domain" {
  log_publishing_options {
    cloudwatch_log_group_arn = "arn:aws:logs:us-east-1:1234:log-group:es-audit-logs"
    log_type = "AUDIT_LOGS"
    enabled = false # Sensitive
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/cloudfront/">Amazon CloudFront</a> distributions:</p>
<pre>
resource "aws_cloudfront_distribution" "cloudfront_distribution" { # Sensitive
  default_root_object = "index.html"
}
</pre>
<p>For both Amazon <a href="https://aws.amazon.com/elasticloadbalancing/classic-load-balancer/">Classic Load Balancing</a> and <a
href="https://aws.amazon.com/elasticloadbalancing/application-load-balancer/">Application Load Balancing</a>:</p>
<pre>
resource "aws_lb" "load_balancer" {
  access_logs {
    enabled = false # Sensitive
    bucket = "mycompliantbucket"
    bucket_prefix = "log/lb-"
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/storage_bucket/">Cloud Storage service</a>:</p>
<pre>
resource "google_storage_bucket" "example" { # Sensitive
  name     = "example"
  location = "US"
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/compute_region_backend_service/">Region Backend
Service</a>:</p>
<pre>
resource "google_compute_region_backend_service" "example" { # Sensitive
  name                            = "example"
  region                          = "us-central1"
  health_checks                   = [google_compute_region_health_check.region.id]
  connection_draining_timeout_sec = 10
  session_affinity                = "CLIENT_IP"
  load_balancing_scheme           = "EXTERNAL"
  protocol                        = "HTTPS"
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/compute_subnetwork/">VPC Subnetwork</a>:</p>
<pre>
resource "google_compute_subnetwork" "example" { # Sensitive
  name          = "example"
  ip_cidr_range = "10.2.0.0/16"
  region        = "us-central1"
  network       = google_compute_network.custom-test.id
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/sql_database_instance/">SQL Database
Instance</a>:</p>
<pre>
resource "google_sql_database_instance" "example" {
  name             = "example"
  database_version = "POSTGRES_11"
  region           = "us-central1"

  settings { # Sensitive
    tier = "db-f1-micro"
    ip_configuration {
      require_ssl  = true
      ipv4_enabled = true
    }
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/container_cluster/">Kubernetes Engine (GKE)
cluster</a>:</p>
<pre>
resource "google_container_cluster" "example" {
  name               = "example"
  location           = "us-central1-a"
  initial_node_count = 3
  logging_service    = "none" # Sensitive
}
</pre>
<h2>Compliant Solution</h2>
<p>For <a href="https://aws.amazon.com/s3/">Amazon S3 access requests</a>:</p>
<pre>
resource "aws_s3_bucket" "myloggingbucket" {
  bucket = "myloggingbucketname"
  acl    = "log-delivery-write"
}

resource "aws_s3_bucket" "mycompliantbucket" {
  bucket = "mycompliantbucketname"

  logging {
      target_bucket = "myloggingbucketname"
      target_prefix = "log/mycompliantbucket"
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/api-gateway/">Amazon API Gateway</a> stages:</p>
<pre>
resource "aws_api_gateway_stage" "api-v1" {
  deployment_id = aws_api_gateway_deployment.example.id
  rest_api_id   = aws_api_gateway_rest_api.example.id
  stage_name    = "v1-prod-api"
  xray_tracing_enabled = true
  access_log_settings {
    destination_arn = "arn:aws:logs:eu-west-1:123456789:test"
    format = "..."
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/neptune/">Amazon Neptune</a> clusters:</p>
<pre>
resource "aws_neptune_cluster" "cluster" {
  enable_cloudwatch_logs_exports = ["audit"]
}
</pre>
<p>For <a href="https://aws.amazon.com/msk/">Amazon MSK</a> broker logs:</p>
<pre>
resource "aws_msk_cluster" "sensitive_msk" {
  cluster_name = "sensitive_msk"
  logging_info {
    broker_logs {
      firehose {
        enabled = false
      }
      s3 {
        enabled = true
        bucket  = "myloggingbucketname"
        prefix  = "log/msk-"
      }
    }
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/amazon-mq/">Amazon MQ</a> enable <code>audit</code> or <code>general</code>:</p>
<pre>
resource "aws_mq_broker" "broker" {
  logs {
    audit = true
    general = true
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/documentdb/">Amazon DocumentDB</a>:</p>
<pre>
resource "aws_docdb_cluster" "docdb_omitting_logs" {
  cluster_identifier = "DB Cluster With Logs"
  enabled_cloudwatch_logs_exports = ["audit"]
}
</pre>
<p>For <a href="https://aws.amazon.com/redshift/">Amazon Redshift</a>:</p>
<pre>
resource "aws_redshift_cluster" "cluster" {
  cluster_identifier = "compliant-redshift-cluster"
  logging {
    enable           = true
    bucket_name      = "infra_logs"
    s3_key_prefix    = "log/redshift-"
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/global-accelerator/">Amazon Global Accelerator</a>:</p>
<pre>
resource "aws_globalaccelerator_accelerator" "accelerator" {
  attributes {
    flow_logs_enabled   = true
    flow_logs_s3_bucket = "example-bucket"
    flow_logs_s3_prefix = "flow-logs/"
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/opensearch-service/">Amazon OpenSearch</a> service, or Amazon Elasticsearch service:</p>
<pre>
resource "aws_elasticsearch_domain" "domain" {
  log_publishing_options {
    cloudwatch_log_group_arn = "arn:aws:logs:us-east-1:1234:log-group:es-audit-logs"
    log_type = "AUDIT_LOGS"
    enabled = true
  }
}
</pre>
<p>For <a href="https://aws.amazon.com/cloudfront/">Amazon CloudFront</a> distributions:</p>
<pre>
resource "aws_cloudfront_distribution" "cloudfront_distribution" {
  default_root_object = "index.html"
  logging_config {
    bucket          = "mycompliantbucketname"
    prefix          = "log/cloudfront-"
  }
}
</pre>
<p>For both Amazon <a href="https://aws.amazon.com/elasticloadbalancing/classic-load-balancer/">Classic Load Balancing</a> and <a
href="https://aws.amazon.com/elasticloadbalancing/application-load-balancer/">Application Load Balancing</a>:</p>
<pre>
resource "aws_lb" "load_balancer" {
  access_logs {
    enabled = true
    bucket = "mycompliantbucket"
    bucket_prefix = "log/lb-"
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/storage_bucket/">Cloud Storage service</a>:</p>
<pre>
resource "google_storage_bucket" "example" {
  name     = "example"
  location = "US"
  logging {
    log_bucket = google_storage_bucket.bucket-log.name
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/compute_region_backend_service/">Region Backend
Service</a>:</p>
<pre>
resource "google_compute_region_backend_service" "example" {
  name                            = "example"
  region                          = "us-central1"
  health_checks                   = [google_compute_region_health_check.region.id]
  connection_draining_timeout_sec = 10
  session_affinity                = "CLIENT_IP"
  load_balancing_scheme           = "EXTERNAL"
  protocol                        = "HTTPS"

  log_config {
    enable = true
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/compute_subnetwork/">VPC Subnetwork</a>:</p>
<pre>
resource "google_compute_subnetwork" "example" {
  name          = "example"
  ip_cidr_range = "10.2.0.0/16"
  region        = "us-central1"
  network       = google_compute_network.custom-test.id

  log_config {
    aggregation_interval = "INTERVAL_10_MIN"
    flow_sampling        = 0.5
    metadata             = "INCLUDE_ALL_METADATA"
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/sql_database_instance/">SQL Database
Instance</a>:</p>
<pre>
resource "google_sql_database_instance" "example" {
  name             = "example"
  database_version = "POSTGRES_11"
  region           = "us-central1"

  settings {
    tier = "db-f1-micro"
    ip_configuration {
      require_ssl  = true
      ipv4_enabled = true
    }
    database_flags {
      name  = "log_connections"
      value = "on"
    }
    database_flags {
      name  = "log_disconnections"
      value = "on"
    }
    database_flags {
      name  = "log_checkpoints"
      value = "on"
    }
    database_flags {
      name  = "log_lock_waits"
      value = "on"
    }
  }
}
</pre>
<p>For GCP <a href="https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/container_cluster/">Kubernetes Engine (GKE)
cluster</a>:</p>
<pre>
resource "google_container_cluster" "example" {
  name               = "example"
  location           = "us-central1-a"
  initial_node_count = 3
  logging_service    = "logging.googleapis.com/kubernetes"
}
</pre>
<h2>See</h2>
<ul>
  <li> <a href="https://owasp.org/Top10/A09_2021-Security_Logging_and_Monitoring_Failures/">OWASP Top 10 2021 Category A9</a> - Security Logging and
  Monitoring Failures </li>
  <li> <a href="https://docs.aws.amazon.com/AmazonS3/latest/userguide/ServerLogs.html">AWS Documentation</a> - Logging requests using server access
  logging </li>
  <li> <a href="https://cwe.mitre.org/data/definitions/778.html">MITRE, CWE-778</a> - Insufficient Logging </li>
  <li> <a href="https://owasp.org/www-project-top-ten/2017/A10_2017-Insufficient_Logging%2526Monitoring">OWASP Top 10 2017 Category A10</a> -
  Insufficient Logging &amp; Monitoring </li>
</ul>

